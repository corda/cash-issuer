buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.1'
    }
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'project-report'
apply plugin: 'application'

sourceSets {
    main {
        resources {
            srcDir "config/dev"
        }
    }
    test {
        resources {
            srcDir "config/test"
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "junit:junit:$junit_version"

    // Corda integration dependencies
    compile "$corda_release_group:corda-rpc:$corda_release_version"

    // Class path scanning.
    compile "io.github.classgraph:classgraph:4.1.12"

    // JOpt: for command line flags.
    compile "net.sf.jopt-simple:jopt-simple:$jopt_simple_version"

    // OKHttp for HTTP library.
    compile "com.squareup.okhttp3:logging-interceptor:3.9.1"

    // Retrofit for REST API client.
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"
    compile "com.squareup.retrofit2:converter-jackson:$retrofit_version"

    // Logging.
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    compile "org.apache.logging.log4j:log4j-web:$log4j_version"
    compile "org.slf4j:jul-to-slf4j:$slf4j_version"

    // Project dependencies.
    compile project(":common:contracts")
    compile project(":service")

    // Token SDK.
    compile "$tokens_release_group:tokens-workflows:$tokens_release_version"
    compile "$tokens_release_group:tokens-money:$tokens_release_version"

}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.2"
        apiVersion = "1.2"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}

shadowJar {
    baseName = 'issuer-daemon'
    classifier = '0.1'
    version = null
    zip64 true
}

mainClassName  = 'com.allianz.t2i.issuer.daemon.MainKt'

jar {
    manifest {
        attributes 'Main-Class': 'com.allianz.t2i.issuer.daemon.MainKt'
    }
    baseName = project.name
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it).matching {
                exclude 'META-INF/**.RSA'
                exclude 'META-INF/MANIFEST.MF'
                exclude 'META-INF/log4j-provider.properties'
            }
        }
    }
}