buildscript {
    ext.corda_release_group = 'net.corda'
    ext.corda_release_version = '4.0-SNAPSHOT'
    ext.corda_gradle_plugins_version = '4.0.36'
    ext.kotlin_version = '1.2.71'
    ext.junit_version = '4.12'
    ext.quasar_version = '0.7.10'
    ext.retrofit_version = '2.4.0'
    ext.okhttp_version = '3.9.1'
    ext.retrofit_version = '2.4.0'
    ext.jopt_simple_version = '5.0.2'
    ext.slf4j_version = '1.7.25'
    ext.log4j_version = '2.11.1'
    ext.artifactory_contextUrl = 'https://ci-artifactory.corda.r3cev.com/artifactory'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url 'https://kotlin.bintray.com/kotlinx'
        }
        maven {
            url "$artifactory_contextUrl/corda-releases"
        }
        maven { 
            url "$artifactory_contextUrl/list/corda-dev/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/list/corda-dev/' }
    maven { url 'https://jitpack.io' }
    maven { url 'http://bluebank-nexus.uksouth.cloudapp.azure.com/repository/maven-public/'}

}

apply plugin: 'kotlin'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'

sourceSets {
    main {
        resources {
            srcDir "config/dev"
        }
    }
    test {
        resources {
            srcDir "config/test"
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

//configurations {
//    integrationTestCompile.extendsFrom testCompile
//    integrationTestRuntime.extendsFrom testRuntime
//}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "junit:junit:$junit_version"

    // Corda integration dependencies
    cordaCompile "$corda_release_group:corda-core:$corda_release_version"
    cordaCompile "$corda_release_group:corda-finance:$corda_release_version"
    cordaCompile "$corda_release_group:corda-jackson:$corda_release_version"
    cordaCompile "$corda_release_group:corda-rpc:$corda_release_version"
    cordaCompile "$corda_release_group:corda-node-api:$corda_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"

    // CorDapp dependencies
    // Specify your CorDapp's dependencies below, including dependent CorDapps.
    // We've defined Cash as a dependent CorDapp as an example.
    cordapp "$corda_release_group:corda-finance:$corda_release_version"
    cordapp project(":common")
    cordapp project(":daemon")
    cordapp project(":client")
    cordapp project(":service")
}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.2"
        apiVersion = "1.2"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}

cordapp {
    info {
        targetPlatformVersion 4
        minimumPlatformVersion 4
    }
}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    directory "./build/nodes"
    node {
        name "O=Notary,L=London,C=GB"
        notary = [validating : false]
        p2pPort 10002
        rpcSettings {
            address("localhost:10003")
            adminAddress("localhost:10043")
        }
        cordapps = []
    }
    node {
        name "O=Issuer,L=London,C=GB"
        p2pPort 10005
        rpcSettings {
            address("localhost:10006")
            adminAddress("localhost:10046")
        }
        cordapp project(":common")
        cordapp project(":service")
        cordapp "$corda_release_group:corda-finance:$corda_release_version"
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
    node {
        name "O=PartyA,L=New York,C=US"
        p2pPort 10008
        rpcSettings {
            address("localhost:10009")
            adminAddress("localhost:10049")
        }
        cordapp project(":common")
        cordapp project(":client")
        cordapp "$corda_release_group:corda-finance:$corda_release_version"
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
    node {
        name "O=PartyB,L=New York,C=US"
        p2pPort 10011
        rpcSettings {
            address("localhost:10012")
            adminAddress("localhost:10052")
        }
        cordapp project(":common")
        cordapp project(":client")
        cordapp "$corda_release_group:corda-finance:$corda_release_version"
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
}
